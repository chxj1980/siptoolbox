mode (-1);
deff('[] = clrscr()', 'clc()')
clrscr()

// TODO turn on double-buffering:
// Old way:
//    prev_pixmap_mode = xget('pixmap');
//    xset('pixmap',1);
// New way (perhaps put it inside imshow):
//    f = gcf();
//    f.pixmap = 'on'; // double buffer mode
//    show_pixmap() // send buffer to screen

DIRINI = PWD
chdir (SIPDIR+'images');
mode (3);
// 
//          SIP - SCILAB IMAGE PROCESSING TOOLBOX
//                    Introductory Demo
//    This demo assumes your scilab window has at least 24
// lines and 75 rows of text. 
// Each time you see a "halt-->"  prompt, press <ENTER> to 
// continue. 
// To quit this demo, type <CTRL-C> followed by "abort" 
// then press <ENTER>.
// Please send comments to Ricardo Fabbri <rfabbri@gmail.com>
//
halt()
clrscr()

//
// SIP is able to read and write image files in practically 
// ANY image format.
// The command
// imread(filename)
// reads images into Scilab matrices:
[Im, map] = imread('example.bmp');    // a color image 
clf
imshow(Im, map);      // plots the image in scilab
halt()
clrscr()

//
// SIP can also write to image files in many formats,
// including BMP, GIF, JPEG, PNG, PCX, TIFF and  XPM:
//
chdir(TMPDIR);
imwrite(Im, map, 'Only_a_Test_Image.tif');
clf
halt()
imshow('Only_a_Test_Image.tif');
halt()
chdir (SIPDIR+'images');
clrscr()

//
//          BINARY IMAGES
//
//
[Im] = imread('bin.xpm')   
clf
imshow(Im,2);
halt()
// note the correspondence between the numerical entries 
// and the plotted image.
halt()
clrscr()

//
//          PSEUDOCOLOR IMAGES 
//
//
[Im, map] = imread('example.bmp');    // a color image 
clf
imshow(Im, map);      // plots the image in scilab
halt()
clrscr()

// 
// The way a pseudocolor image is displayed depends on 
// its colormap. The command:
imshow(Im,graycolormap(256));
// shows Im using gray shades. Im is a standard 2D array,
// with entries ranging from 1 to 256. These are indexes into
// the colormap. All that was done here was to replace the 
// original colormap, "map", by a grayscale one. In this
// scheme, the brightness of the image is proportional to
// the index.
halt()
clrscr()

//
// There are other visually interesting colormaps, used to
// enhance certain features of the image. For example:
//
imshow(Im, hotcolormap(256)); 
// where the color ranges from black (index 1) to shades of red 
// and yellow to white (256)
halt()
clrscr()

// 
// A colormap is an Mx3 matrix of entries ranging from 0 to
// 1 . Each column corresponds to red, green and blue color 
// intensities. Each row defines a // color to be indexed by 
// an  index matrix. The Scilab functions "graycolormap" and 
// "hotcolormap" simply generate colormaps with the desired 
// number of rows (colors). For example,

Im(20,35)          // a pixel contains a colormap index
halt()
// the R G B intensities for that pixel (0-1):
map(Im(20,35)+1, :)  
halt()
clrscr()

// A colormap is a matrix, so it is possible to apply
// operations to them, and obtain interesting visualization 
// effects:
cm = (hotcolormap(256) + graycolormap(256)) / 2;
imshow(Im, cm)
// that smoothed the chromaticity of the hot colormap.
halt()
imshow(Im, cm.^0.5)
// that brightened the image. Since "cm" ranges from 0.0 to
// 1.0, raising it to a power smaller than 1 will increase the
// intensities. 
halt()

// Other examples:
imshow(Im, 1-hotcolormap(256))    // a "cold" colormap
halt()
imshow(Im, 1 - map)    // inverse of the original image
halt()
clrscr()

//
//          TRUECOLOR IMAGES 
//
//
[Im] = imread('tru.jpg');   // 'Im' is in 0-1 range
clf
imshow(Im);   // imshow NOW assumes 0-1 range for truecolor
halt()
// Truecolor images are stored as 3D hypermatrices:
size(Im)
Im(21,45,3)     // pixel (21,45) of blue channel
halt()
clrscr()

//
// Im(:,:,1) is the red channel array
// Im(:,:,2) is the green channel
// Im(:,:,3) is the blue channel
//
imshow(Im(:,:,2))   // shows the green channel in grayscale
halt();
clrscr()

//       
//          SIP - SCILAB IMAGE PROCESSING TOOLBOX
//                       END OF DEMO       
//   For more information, refer to the help pages for: imread,
//   imwrite, imshow, and imfinfo functions. Also follow the
//   EXAMPLE sections in the manpages of SIP functions.
//    
//   This demo was written by:
//   Ricardo Fabbri <rfabbri@gmail.com>
//
halt()
chdir (DIRINI);
clear Im map DIRINI
